plugins {
    id 'java-library'
    id 'org.ajoberstar.grgit' version '5.0.0'
    id 'org.ajoberstar.reckon' version '0.16.1'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'maven-publish'
    id 'signing'
}

group = 'com.github.moaxcp'
description = 'gif builder'

reckon {
    snapshots()
    scopeCalc = calcScopeFromProp().or(calcScopeFromCommitMessages())
    stageCalc = calcStageFromProp()
}

javadoc {
    options.addBooleanOption('html5', true)
    options.links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
}

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
}

sourceSets {
    example {
        java {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
        }
    }
    main {
        java{
            srcDirs "$buildDir/generated/sources/annotationProcessor/java/main"
        }
    }
}

configurations {
    exampleImplementation.extendsFrom(implementation)
}

dependencies {
    annotationProcessor 'org.immutables:value:2.9.0'
    compileOnly 'org.immutables:value:2.9.0'
    implementation 'com.squareup:gifencoder:0.10.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.jmockit:jmockit:1.49'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
    jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}"
}

nexusPublishing {
    repositories {
        sonatype {
            //by default value from above 'nexusStaging' closure is used (alternative project properties 'sonatypeUsername' and 'sonatypePassword')
            username = project.findProperty("nexus.username")
            password = project.findProperty("nexus.password")
        }
    }

    clientTimeout = Duration.ofSeconds(300)
    connectTimeout = Duration.ofSeconds(60)

}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/moaxcp/gif-builder'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/moaxcp/gif-builder.git'
                    developerConnection = 'scm:git:git@github.com:moaxcp/gif-builder.git'
                    url = 'https://github.com/moaxcp/gif-builder'
                }
            }
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/moaxcp/gif-builder")
                    credentials {
                        username = project.findProperty('github.user')
                        password = project.findProperty('github.key')
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}